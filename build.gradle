// Top-level build file where you can add configuration options common to all sub-projects/modules.



buildscript {
    ext.kotlin_version = '2.1.10'
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://developer.huawei.com/repo/'
        }
        maven {
            url 'https://developer.hihonor.com/repo'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.10.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.huawei.agconnect:agcp:1.9.1.301'
        classpath 'com.hihonor.mcs:asplugin:2.0.1.300'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://developer.huawei.com/repo/'
        }
        maven {
            url 'https://developer.hihonor.com/repo'
        }
        flatDir {
            dirs rootProject.file('client/mixpush-mi/libs'), rootProject.file('client/mixpush-oppo/libs'), rootProject.file('client/mixpush-vivo/libs')
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//subprojects {
//    apply plugin: 'maven'
//    uploadArchives {
//        repositories {
//            mavenDeployer {
////                repository(url: uri('/Users/Wiki/Downloads/repo'))
//            }
//        }
//    }
//}
allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }
}

// 为 JitPack 提供聚合所有子模块的 Maven 发布配置
apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.beiguancyc'
            artifactId = 'mixpush'
            version = project.hasProperty('version') ? project.version : '1.0.0'
            
            // 聚合所有子模块作为依赖
            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    
                    // 添加所有子模块作为依赖
                    def addDependency = { groupId, artifactId, version ->
                        def dependency = dependenciesNode.appendNode('dependency')
                        dependency.appendNode('groupId', groupId)
                        dependency.appendNode('artifactId', artifactId)
                        dependency.appendNode('version', version)
                        dependency.appendNode('scope', 'runtime')
                    }
                    
                    def ver = project.hasProperty('version') ? project.version : '1.0.0'
                    addDependency('com.github.beiguancyc.mixpush', 'mixpush-core', ver)
                    addDependency('com.github.beiguancyc.mixpush', 'mixpush-mi', ver)
                    addDependency('com.github.beiguancyc.mixpush', 'mixpush-huawei', ver)
                    addDependency('com.github.beiguancyc.mixpush', 'mixpush-honor', ver)
                    addDependency('com.github.beiguancyc.mixpush', 'mixpush-oppo', ver)
                    addDependency('com.github.beiguancyc.mixpush', 'mixpush-vivo', ver)
                    addDependency('com.github.beiguancyc.mixpush', 'mixpush-meizu', ver)
                    addDependency('com.github.beiguancyc.mixpush', 'mixpush-fcm', ver)
                }
            }
        }
    }
}
